plugins {
    id 'java-library'
    id 'dev.architectury.loom' version '1.4.+'
    id 'org.ajoberstar.reckon' version '0.13.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

group = project.group
description = project.displayname
base.archivesName = project.name

loom {
    mixin.defaultRefmapName = 'presencefootsteps.mixin.refmap.json'

    forge {
        mixinConfigs = ['presencefootsteps.mixin.json']
    }

    silentMojangMappingsLicense()
}

reckon {
    scopeFromProp()
    stageFromProp('beta', 'rc', 'final')
}

repositories {
    maven { name 'minelp'; url 'https://repo.minelittlepony-mod.com/maven/snapshot' }
    maven { name 'minelp-release'; url 'https://repo.minelittlepony-mod.com/maven/release' }
}

dependencies {
    minecraft "com.mojang:minecraft:1.20.1"
    mappings loom.officialMojangMappings()

    forge "net.minecraftforge:forge:1.20.1-47.2.19"
}

tasks.named('processResources', ProcessResources) {
    inputs.property "version", project.version.toString()

    from 'LICENSE'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}
